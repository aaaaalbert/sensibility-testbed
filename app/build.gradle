apply plugin: 'com.android.model.application'

def lib_distribution_root = '../distribution'

model {
    repositories {
        libs(PrebuiltLibraries) {
            python27 {
                // Python header files
                headers.srcDir "${lib_distribution_root}/python27/include"
                // Python shared library -- each ABI should have a lib file
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/python27/lib/${targetPlatform.getName()}/libpython2.7.so")
                }
            }
        }
    }

    android {
        compileSdkVersion 21
        buildToolsVersion "25.0.0"

        defaultConfig {
            applicationId = "com.sensibility_testbed"
            minSdkVersion.apiLevel = 21
            targetSdkVersion.apiLevel = 24
            versionCode = 17
            versionName = "20170308-1040-CET"
        }
        ndk {
            platformVersion 21
            moduleName "snakei"
            toolchain = 'clang'
            abiFilters.addAll(['armeabi-v7a'])
            ldLibs.addAll(["android", "log"])
        }
        sources {
            main {
                jni {
                    dependencies {
                        library 'python27' linkage 'shared'
                    }
                }
                jniLibs {
                    source {
                        srcDir "${lib_distribution_root}/python27/lib"
                    }
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles.add(file('proguard-android.txt'))
                // NOTE: We MUST use debuggable builds at the moment, see
                // https://github.com/aaaaalbert/sensibility-testbed/issues/30
                debuggable true
            }
            // debug build type is implicit
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.google.android.gms:play-services-location:9.0.0'
    // compile 'com.google.guava:guava:18.0'
    // compile files('libs/libGoogleAnalytics.jar')
    // compile files('libs/locale_platform.jar')
    // compile files('libs/script.jar')
}
